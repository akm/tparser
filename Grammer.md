# Object Pascal grammar

See also [Object Pascal Guide](https://docs.embarcadero.com/products/rad_studio/cbuilder6/EN/CB6_ObjPascalLangGuide_EN.pdf) in [RAD Studio documents](https://docs.embarcadero.com/products/rad_studio/)

| Mark | State       | Count |
| :--: | ----------- | ----: |
|  ğŸ”–  | TODO        |    30 |
|  ğŸš§  | In progress |     6 |
|  âœ”ï¸  | Done        |    86 |

- Goal ğŸš§
  ```
  (Program | Package | Library | Unit)
  ```
- Program âœ”ï¸
  ```
  [PROGRAM Ident ['(' IdentList ')'] ';']
  ProgramBlock '.'
  ```
- Unit âœ”ï¸
  ```
  UNIT Ident [PortabilityDirective] ';'
  InterfaceSection
  ImplementationSection
  [InitSection] '.'
  ```
- Package ğŸ”–
  ```
  PACKAGE Ident ';'
  [RequiresClause]
  [ContainsClause]
  END '.'
  ```
- Library ğŸ”–
  ```
  LIBRARY Ident ';'
  ProgramBlock '.'
  ```
- ProgramBlock âœ”ï¸
  ```
  [UsesClause]
  Block
  ```
- UsesClause âœ”ï¸
  ```
  USES IdentList ';'
  ```
- PortabilityDirective ğŸš§
  ```
  platform
  ```
  ```
  deprecated
  ```
  ```
  library
  ```
- InterfaceSection âœ”ï¸
  ```
  INTERFACE
  [UsesClause]
  [InterfaceDecl]...
  ```
- InterfaceDecl âœ”ï¸
  ```
  ConstSection
  ```
  ```
  TypeSection
  ```
  ```
  VarSection
  ```
  ```
  ExportedHeading
  ```
- ExportedHeading âœ”ï¸
  ```
  ProcedureHeading ';' [Directive]
  ```
  ```
  FunctionHeading ';' [Directive]
  ```
- ImplementationSection âœ”ï¸
  ```
  IMPLEMENTATION
  [UsesClause]
  [DeclSection]...
  [ExportsStmt]...
  ```
- Block âœ”ï¸
  ```
  [DeclSection]
  [ExportsStmt]...
  BlockBody
  [ExportsStmt]...
  ```
- BlockBody âœ”ï¸
  ```
  CompoundStmt
  ```
  ```
  AssemberStatement
  ```
- ExportsStmt âœ”ï¸
  ```
  EXPORTS ExportsItem [, ExportsItem]...
  ```
- ExportsItem âœ”ï¸
  ```
  Ident [NAME|INDEX â€œâ€˜â€ ConstExpr â€œâ€˜â€]
        [INDEX|NAME â€œâ€˜â€ ConstExpr â€œâ€˜â€]
  ```
- DeclSection âœ”ï¸
  ```
  LabelDeclSection
  ```
  ```
  ConstSection
  ```
  ```
  TypeSection
  ```
  ```
  VarSection
  ```
  ```
  ProcedureDeclSection
  ```
- LabelDeclSection âœ”ï¸
  ```
  LABEL LabelId ';'
  ```
- ConstSection âœ”ï¸
  ```
  CONST (ConstantDecl ';')...
  ```
- ConstantDecl âœ”ï¸
  ```
  Ident '=' ConstExpr [PortabilityDirective]
  ```
  ```
  Ident ':' TypeId '=' TypedConstant [PortabilityDirective]
  ```
- TypeSection âœ”ï¸
  ```
  TYPE (TypeDecl ';')...
  ```
- TypeDecl âœ”ï¸
  ```
  Ident '=' [TYPE] Type [PortabilityDirective]
  ```
  ```
  Ident '=' [TYPE] RestrictedType [PortabilityDirective]
  ```
- TypedConstant ğŸ”–
  ```
  (ConstExpr | ArrayConstant | RecordConstant)
  ```
- ArrayConstant ğŸ”–
  ```
  '(' TypedConstant ',' ')'
  ```
- RecordConstant ğŸ”–
  ```
  '(' RecordFieldConstant ';'... ')'
  ```
- RecordFieldConstant ğŸ”–
  ```
  Ident ':' TypedConstant
  ```
- Type ğŸš§
  ```
  TypeId
  ```
  ```
  SimpleType
  ```
  ```
  StrucType
  ```
  ```
  PointerType
  ```
  ```
  StringType
  ```
  ```
  ProcedureType
  ```
  ```
  VariantType
  ```
  ```
  ClassRefType
  ```
- RestrictedType ğŸ”–
  ```
  ObjectType
  ```
  ```
  ClassType
  ```
  ```
  InterfaceType
  ```
- ClassRefType ğŸ”–
  ```
  CLASS OF TypeId
  ```
- SimpleType âœ”ï¸
  ```
  (OrdinalType | RealType)
  ```
- RealType âœ”ï¸
  ```
  REAL48
  ```
  ```
  REAL
  ```
  ```
  SINGLE
  ```
  ```
  DOUBLE
  ```
  ```
  EXTENDED
  ```
  ```
  CURRENCY
  ```
  ```
  COMP
  ```
- OrdinalType âœ”ï¸
  ```
  (SubrangeType | EnumeratedType | OrdIdent)
  ```
- OrdIdent âœ”ï¸
  ```
  SHORTINT
  ```
  ```
  SMALLINT
  ```
  ```
  INTEGER
  ```
  ```
  BYTE
  ```
  ```
  LONGINT
  ```
  ```
  INT64
  ```
  ```
  WORD
  ```
  ```
  BOOLEAN
  ```
  ```
  CHAR
  ```
  ```
  WIDECHAR
  ```
  ```
  LONGWORD
  ```
  ```
  PCHAR
  ```
- VariantType âœ”ï¸
  ```
  VARIANT
  ```
  ```
  OLEVARIANT
  ```
- SubrangeType âœ”ï¸
  ```
  ConstExpr '..' ConstExpr
  ```
- EnumeratedType âœ”ï¸
  ```
  '(' EnumeratedTypeElement ','... ')'
  ```
- EnumeratedTypeElement âœ”ï¸
  ```
  Ident [ '=' ConstExpr ]
  ```
- StringType âœ”ï¸
  ```
  STRING
  ```
  ```
  ANSISTRING
  ```
  ```
  WIDESTRING
  ```
  ```
  STRING '[' ConstExpr ']'
  ```
- StrucType âœ”ï¸
  ```
  [PACKED] (ArrayType [PACKED]| SetType | FileType | RecType [PACKED])
  ```
- ArrayType âœ”ï¸
  ```
  ARRAY ['[' OrdinalType ','... ']'] OF Type [PortabilityDirective]
  ```
- RecType âœ”ï¸
  ```
  RECORD [FieldList] END [PortabilityDirective]
  ```
- FieldList âœ”ï¸
  ```
  FieldDecl ';'... [VariantSection] [';']
  ```
- FieldDecl âœ”ï¸
  ```
  IdentList ':' Type [PortabilityDirective]
  ```
- VariantSection âœ”ï¸
  ```
  CASE [Ident ':'] TypeId OF RecVariant ';'...
  ```
- RecVariant âœ”ï¸
  ```
  ConstExpr ','... ':' '(' [FieldList] ')'
  ```
- SetType âœ”ï¸
  ```
  SET OF OrdinalType [PortabilityDirective]
  ```
- FileType âœ”ï¸
  ```
  FILE OF TypeId [PortabilityDirective]
  ```
- PointerType âœ”ï¸
  ```
  '^' TypeId [PortabilityDirective]
  ```
- ProcedureType âœ”ï¸
  ```
  (ProcedureHeading | FunctionHeading) [OF OBJECT]
  ```
- VarSection âœ”ï¸
  ```
  VAR (VarDecl ';')...
  ```
- VarDecl âœ”ï¸
  - (On Windows)
    ```
    IdentList ':' Type [(ABSOLUTE (Ident | ConstExpr)) | '=' ConstExpr] [PortabilityDirective]
    ```
  - On Linux
    ```
    IdentList ':' Type [ABSOLUTE (Ident) | '=' ConstExpr] [PortabilityDirective]
    ```
- Expression âœ”ï¸
  ```
  SimpleExpression [RelOp SimpleExpression]...
  ```
- SimpleExpression âœ”ï¸
  ```
  ['+' | '-'] Term [AddOp Term]...
  ```
- Term âœ”ï¸
  ```
  Factor [MulOp Factor]...
  ```
- Factor âœ”ï¸
  ```
  Designator ['(' ExprList ')']
  ```
  ```
  '@' Designator
  ```
  ```
  Number
  ```
  ```
  String
  ```
  ```
  NIL
  ```
  ```
  '(' Expression ')'
  ```
  ```
  NOT Factor
  ```
  ```
  SetConstructor
  ```
  ```
  TypeId '(' Expression ')'
  ```
- RelOp âœ”ï¸
  ```
  '>'
  ```
  ```
  '<'
  ```
  ```
  '<='
  ```
  ```
  '>='
  ```
  ```
  '='
  ```
  ```
  '<>'
  ```
  ```
  IN
  ```
  ```
  IS
  ```
- AddOp âœ”ï¸
  ```
  '+'
  ```
  ```
  '-'
  ```
  ```
  OR
  ```
  ```
  XOR
  ```
- MulOp âœ”ï¸
  ```
  '*'
  ```
  ```
  '/'
  ```
  ```
  DIV
  ```
  ```
  MOD
  ```
  ```
  AND
  ```
  ```
  SHL
  ```
  ```
  SHR
  ```
  ```
  AS
  ```
- Designator âœ”ï¸
  ```
  QualId ['.' Ident | '[' ExprList ']' | '^']...
  ```
- SetConstructor âœ”ï¸
  ```
  '[' [SetElement ','...] ']'
  ```
- SetElement âœ”ï¸
  ```
  Expression ['..' Expression]
  ```
- ExprList âœ”ï¸
  ```
  Expression ','...
  ```
- Statement âœ”ï¸
  ```
  [LabelId ':'] [SimpleStatement | StructStmt]
  ```
- StmtList âœ”ï¸
  ```
  (Statement ';') ...
  ```
- SimpleStatement âœ”ï¸
  ```
  Designator ['(' [ExprList] ')']
  ```
  ```
  Designator ':=' Expression
  ```
  ```
  INHERITED
  ```
  ```
  GOTO LabelId
  ```
- StructStmt âœ”ï¸
  ```
  CompoundStmt
  ```
  ```
  ConditionalStmt
  ```
  ```
  LoopStmt
  ```
  ```
  WithStmt
  ```
  ```
  TryExceptStmt
  ```
  ```
  TryFinallyStmt
  ```
  ```
  RaiseStmt
  ```
  ```
  AssemblerStmt
  ```
- CompoundStmt âœ”ï¸
  ```
  BEGIN StmtList END
  ```
- ConditionalStmt âœ”ï¸
  ```
  IfStmt
  ```
  ```
  CaseStmt
  ```
- IfStmt âœ”ï¸
  ```
  IF Expression THEN Statement [ELSE Statement]
  ```
- CaseStmt âœ”ï¸
  ```
  CASE Expression OF CaseSelector ';'... [ELSE StmtList] [';'] END
  ```
- CaseSelector âœ”ï¸
  ```
  CaseLabel ','... ':' Statement
  ```
- CaseLabel âœ”ï¸
  ```
  ConstExpr ['..' ConstExpr]
  ```
- LoopStmt âœ”ï¸
  ```
  RepeatStmt
  ```
  ```
  WhileStmt
  ```
  ```
  ForStmt
  ```
- RepeatStmt âœ”ï¸
  ```
  REPEAT StmtList UNTIL Expression
  ```
- WhileStmt âœ”ï¸
  ```
  WHILE Expression DO Statement
  ```
- ForStmt âœ”ï¸
  ```
  FOR QualId ':=' Expression (TO | DOWNTO) Expression DO Statement
  ```
- WithStmt ğŸš§
  ```
  WITH IdentList DO Statement
  ```
- TryExceptStmt âœ”ï¸
  ```
  TRY
    Statement...
  EXCEPT
    ExceptionBlock
  END
  ```
- ExceptionBlock âœ”ï¸
  ```
  [ON [Ident â€˜:â€™] TypeID DO Statement]...
  [ELSE Statement...]
  ```
- TryFinallyStmt âœ”ï¸
  ```
  TRY
    Statement
  FINALLY
    Statement
  END
  ```
- RaiseStmt âœ”ï¸
  ```
  RAISE [object] [AT address]
  ```
- AssemblerStatement âœ”ï¸
  ```
  ASM
  <assemblylanguage>
  END
  ```
- ProcedureDeclSection âœ”ï¸
  ```
  ProcedureDecl
  ```
  ```
  FunctionDecl
  ```
- ProcedureDecl âœ”ï¸
  ```
  ProcedureHeading ';' [Directive] [PortabilityDirective]
  Block ';'
  ```
- FunctionDecl âœ”ï¸
  ```
  FunctionHeading ';' [Directive] [PortabilityDirective]
  Block ';'
  ```
- FunctionHeading âœ”ï¸
  ```
  FUNCTION Ident [FormalParameters] ':' (SimpleType | STRING)
  ```
  (Actually ReturnType is not only SimpleType or STRING.
  TypeId also can be also.)
- ProcedureHeading âœ”ï¸
  ```
  PROCEDURE Ident [FormalParameters]
  ```
- FormalParameters âœ”ï¸
  ```
  '(' [FormalParm ';'...] ')'
  ```
- FormalParm âœ”ï¸
  ```
  [VAR | CONST | OUT] Parameter
  ```
- Parameter âœ”ï¸
  ```
  IdentList [':' ([ARRAY OF] SimpleType | STRING | FILE)]
  ```
  (Parameter type is not only SimpleType, STRING or FILE.
  TypeId also can be also.)
  ```
  Ident ':' SimpleType '=' ConstExpr
  ```
- Directive ğŸš§
  ```
  CDECL
  ```
  ```
  REGISTER
  ```
  ```
  DYNAMIC
  ```
  ```
  VIRTUAL
  ```
  ```
  EXPORT
  ```
  ```
  EXTERNAL
  ```
  ```
  NEAR
  ```
  ```
  FAR
  ```
  ```
  FORWARD
  ```
  ```
  MESSAGE ConstExpr
  ```
  ```
  OVERRIDE
  ```
  ```
  OVERLOAD
  ```
  ```
  PASCAL
  ```
  ```
  REINTRODUCE
  ```
  ```
  SAFECALL
  ```
  ```
  STDCALL
  ```
  ```
  VARARGS
  ```
  ```
  LOCAL
  ```
  ```
  ABSTRACT
  ```
- InitSection ğŸ”–
  ```
  INITIALIZATION StmtList [FINALIZATION StmtList] END
  ```
  ```
  BEGIN StmtList END
  ```
  ```
  END
  ```
- ClassType ğŸ”–
  ```
  CLASS [ClassHeritage]
  [ClassMemberSections]
  END
  ```
- ObjectType ğŸ”–
  ```
  OBJECT [ClassHeritage]
  [ClassMemberSections]
  END
  ```
- ClassHeritage ğŸ”–
  ```
  '(' TypeId ',' ... ')'
  ```
- ClassMemberSections
  ```
  ClassMemberSection ...
  ```
- ClassMemberSection
  ```
  ClassVisibility
  [ClassFieldList]
  [ClassMethodList]
  [ClassPropertyList]
  ```
- ClassVisibility ğŸ”–
  ```
  [PUBLIC | PROTECTED | PRIVATE | PUBLISHED]
  ```
- ClassFieldList ğŸ”–
  ```
  ClassField ';' ...
  ```
- ClassField ğŸ”–
  ```
  IdentList ':' Type
  ```
- ClassMethodList ğŸ”–
  ```
  ClassMethod ';' ...
  ```
- ClassMethod ğŸ”–
  ```
  [CLASS] ClassMethodHeading [';' ClassMethodDirective ...]
  ```
- ClassMethodHeading ğŸ”–
  ```
  ProcedureHeading
  ```
  ```
  FunctionHeading
  ```
  ```
  ConstructorHeading
  ```
  ```
  DestructorHeading
  ```
- ClassMethodDirective
  ```
  ABSTRACT
  ```
  ```
  VIRTUAL
  ```
  ```
  OVERRIDE
  ```
  ```
  OVERLOAD
  ```
  ```
  REINTRODUCE
  ```
- ConstructorHeading ğŸ”–
  ```
  CONSTRUCTOR Ident [FormalParameters]
  ```
- DestructorHeading ğŸ”–
  ```
  DESTRUCTOR Ident [FormalParameters]
  ```
- ClassPropertyList ğŸ”–
  ```
  ClassProperty ';' ...
  ```
- ClassProperty ğŸ”–
  ```
	 PROPERTY Ident
  [PropertyInterface]
  [INDEX ConstExpr]
  [READ Ident]
  [WRITE Ident]
  [STORED (Ident | Constant)]
  [(DEFAULT ConstExpr) | NODEFAULT]
  [IMPLEMENTS TypeId]
  [PortabilityDirective]
  ```
- PropertyInterface ğŸ”–
  ```
  [PropertyParameterList] ':' Ident
  ```
- PropertyParameterList ğŸ”–
  ```
  '[' PropertyParameter ';'... ']'
  ```
- PropertyParameter ğŸ”–
  ```
  IdentList ':' TypeId
  ```
- InterfaceType ğŸ”–
  ```
  INTERFACE
  [InterfaceHeritage]
  [InterfaceGuid]
  [InterfaceMemberList]
  ...
  END
  ```
- InterfaceHeritage ğŸ”–
  ```
  '(' TypeId ',' ... ')'
  ```
- InterfaceGuid
  ```
  '[' ConstExpr of string ']'
  ```
- InterfaceMemberList
  ```
  InterfaceMember ';'...
  ```
- InterfaceMember
  ```
  InterfaceMethod
  ```
  ```
  InterfaceProperty
  ```
- InterfaceMethod
  ```
  InterfaceMethodHeading; [InterfaceMethodDirective ';'...];
  ```
- InterfaceMethodHeading
  ```
  ProcedureHeading
  ```
  ```
  FunctionHeading
  ```
- InterfaceMethodDirective
  ```
  stdcall
  ```
- InterfaceProperty ğŸ”–
  ```
  PROPERTY Ident PropertyInterface PropertyAccessor
  ```
- RequiresClause ğŸ”–
  ```
  REQUIRES IdentList... ';'
  ```
- ContainsClause ğŸ”–
  ```
  CONTAINS IdentList... ';'
  ```
- IdentList âœ”ï¸
  ```
  Ident ','...
  ```
- QualId âœ”ï¸
  ```
  [UnitId '.'] Ident
  ```
- TypeId âœ”ï¸
  ```
  [UnitId '.'] <type-identifier>
  ```
- Ident âœ”ï¸
  ```
  <identifier>
  ```
- ConstExpr ğŸš§
  ```
  <constant-expression>
  ```
- UnitId âœ”ï¸
  ```
  <unit-identifier>
  ```
- LabelId âœ”ï¸
  ```
  <label-identifier>
  ```
- Number âœ”ï¸
  ```
  <number>
  ```
- String âœ”ï¸
  ```
  <string>
  ```
